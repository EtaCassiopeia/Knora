@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and André Fatton.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi._
@import org.knora.webapi.twirl.SparqlTemplateLinkUpdate
@import org.knora.webapi.messages.v1respondermessages.valuemessages._

@**
 * Inserts a new value of a resource property (as opposed to a new version of an existing value).
 *
 * @param dataNamedGraph the named graph in which the project stores its data.
 * @param triplestore the name of the triplestore being used. The template uses this value to exclude inferred
                      results from the WHERE clause of the update.
 * @param resourceIri the resource to update.
 * @param propertyIri the resource property to update.
 * @param newValueIri the new value IRI.
 * @param valueTypeIri the type of the new value.
 * @param permissionRelevantAssertions Permission-relevant assertions, i.e. owner and project plus permissions, in
 *        the form of a list of tuples (predicate, object).
 * @param linkUpdates a list of [[LinkUpdate]] objects describing links and LinkValues that need to be
 *        updated for resource references in Standoff text values.
 * @param maybeComment an optional comment on the value.
 * @param value the value to insert.
 *
 * The generated WHERE clause does some consistency checks. If these fail, the operation will do nothing.
 * To find out whether the update succeeded, the application must query the property's version history.
 *@
@(dataNamedGraph: IRI,
  triplestore: String,
  resourceIri: IRI,
  propertyIri: IRI,
  newValueIri: IRI,
  valueTypeIri: IRI,
  value: UpdateValueV1,
  linkUpdates: Seq[SparqlTemplateLinkUpdate],
  maybeComment: Option[String],
  permissionRelevantAssertions: Seq[(IRI, IRI)])

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix knora-base: <http://www.knora.org/ontology/knora-base#>

DELETE {
    GRAPH ?dataNamedGraph {
        @* Delete the resource's last modification date so we can update it. *@
        ?resource knora-base:lastModificationDate ?resourceLastModificationDate .

        @* If any LinkValues need to be updated, detach them from the resource. *@
        @if(linkUpdates.exists(_.linkValueExists)) {
            @for((linkUpdate, linkValueIndex) <- linkUpdates.zipWithIndex) {
                @if(linkUpdate.linkValueExists) {
                    ?resource <@{linkUpdate.linkPropertyIri}Value> ?linkValue@linkValueIndex .
                }
            }
        }
    }
} INSERT {
    GRAPH ?dataNamedGraph {
        @{
            queries.sparql.v1.txt.generateInsertStatementsForCreateValue(valueIndex = 0,
                                                                         value = value,
                                                                         linkUpdates = linkUpdates,
                                                                         maybeComment = maybeComment,
                                                                         permissionRelevantAssertions = permissionRelevantAssertions)
        }
    }

    ?resource knora-base:lastModificationDate ?currentTime .
}
@*

GraphDB's consistency checking requires reasoning, but reasoning interferes with certain things
in the WHERE clauses of our SPARQL updates, so we set a GraphDB-specific flag to return only
explicit statements in the WHERE clause here.

*@
@triplestore match {
    case "graphdb" | "graphdb-free" => {
        USING <http://www.ontotext.com/explicit>
    }

    case other => {}
}
WHERE {
    BIND(IRI("@resourceIri") AS ?resource)
    BIND(NOW() as ?currentTime)

    @{
        queries.sparql.v1.txt.generateWhereStatementsForCreateValue(valueIndex = 0,
                                                                    resourceIri = resourceIri,
                                                                    propertyIri = propertyIri,
                                                                    newValueIri = newValueIri,
                                                                    valueTypeIri = valueTypeIri,
                                                                    linkUpdates = linkUpdates)
     }

    @* Get the resource's last modification date, if it has one, so we can update it. *@

    OPTIONAL {
        ?resource knora-base:lastModificationDate ?resourceLastModificationDate .
    }
}
